## Findings Summary

| ID | Description | Severity |
| - | - | - |
| [M-01](#m-01-ordermetadata-typehash-is-not-eip712-compliant) | OrderMetadata typeHash is not EIP712 compliant | Medium |
| [M-02](#m-02-nfts-can-be-freely-burned-by-renter-at-low-cost) | NFTs can be freely burned by renter at low cost | Medium |
| [M-03](#m-03-long-period-self-rents-will-block-hook-disabling) | Long period self rents will block hook disabling | Medium |
| [M-04](#m-04-encodeddata-argument-of-ordermetadata-hashstruct-is-missing-struct-members-which-is-not-eip712-compliant) | encodedData argument of OrderMetadata hashStruct is missing struct members which is not EIP712 compliant | Medium |
| [M-05](#m-05-erc721pausable-tokens-can-block-reclaiming) | ERC721Pausable tokens can block reclaiming | Medium |
| [L-01](#l-01-cryptopunk-nfts-will-be-stolen) | CryptoPunk NFTs will be stolen | Low |
| [L-02](#l-02-some-nfts-will-return-the-floor-price-when-burned) | Some NFTs will return the floor price when burned | Low |
| [L-03](#l-03-no-validation-if-orderordertype-and-ordermetadataordertype-are-equal) | No validation if `Order.orderType` and `OrderMetadata.orderType` are equal | Low |

# [M-01] OrderMetadata typeHash is not EIP712 compliant

## Impact

OrderMetadata struct has nested an array of the Hook struct. This Hook struct is missing from the OrderMeta typehash, which makes the OrderMetadata not compliant with EIP712, and with that, the RentPayload is also not compliant because it has OrderMetadata in it. 

## Proof of Concept

The OrderMetadata typehash is composed in `_deriveRentalTypehashes()` as you see, the Hook typestring is missing.
Also, you can see the RentalOrder struct, which has nested structs but composes the typehash correctly.
https://github.com/re-nft/smart-contracts/blob/3ddd32455a849c3c6dc3c3aad7a33a6c9b44c291/src/packages/Signer.sol#L406

```solidity
struct OrderMetadata {
    // Type of order being created.
    OrderType orderType;
    // Duration of the rental in seconds.
    uint256 rentDuration;
    // Hooks that will act as middleware for the items in the order.
    Hook[] hooks;
    // Any extra data to be emitted upon order fulfillment.
    bytes emittedExtraData;
}
```

```solidity
function _deriveRentalTypehashes()
    internal
    pure
    returns (
        bytes32 itemTypeHash,
        bytes32 hookTypeHash,
        bytes32 rentalOrderTypeHash,
        bytes32 orderFulfillmentTypeHash,
        bytes32 orderMetadataTypeHash,
        bytes32 rentPayloadTypeHash
    )
{
    // Construct the Item type string.
    bytes memory itemTypeString = abi.encodePacked(
        "Item(uint8 itemType,uint8 settleTo,address token,uint256 amount,uint256 identifier)"
    );

    // Construct the Hook type string.
    bytes memory hookTypeString = abi.encodePacked(
        "Hook(address target,uint256 itemIndex,bytes extraData)"
    );

    // Construct the RentalOrder type string.
    bytes memory rentalOrderTypeString = abi.encodePacked(
        "RentalOrder(bytes32 seaportOrderHash,Item[] items,Hook[] hooks,uint8 orderType,address lender,address renter,address rentalWallet,uint256 startTimestamp,uint256 endTimestamp)"
    );

    // Derive the Item type hash using the corresponding type string.
    itemTypeHash = keccak256(itemTypeString);

    // Derive the Hook type hash using the corresponding type string.
    hookTypeHash = keccak256(hookTypeString);

    // Derive the RentalOrder type hash using the corresponding type string.
    rentalOrderTypeHash = keccak256(
        abi.encode(rentalOrderTypeString, hookTypeString, itemTypeString)
    );

    {
        // Construct the OrderFulfillment type string.
        bytes memory orderFulfillmentTypeString = abi.encodePacked(
            "OrderFulfillment(address recipient)"
        );

        // Construct the OrderMetadata type string.
        bytes memory orderMetadataTypeString = abi.encodePacked(
            "OrderMetadata(uint8 orderType,uint256 rentDuration,Hook[] hooks,bytes emittedExtraData)"
        );

        // Construct the RentPayload type string.
        bytes memory rentPayloadTypeString = abi.encodePacked(
            "RentPayload(OrderFulfillment fulfillment,OrderMetadata metadata,uint256 expiration,address intendedFulfiller)"
        );

        // Derive RentPayload type hash via combination of relevant type strings.
        rentPayloadTypeHash = keccak256(
            abi.encodePacked(
                rentPayloadTypeString,
                orderMetadataTypeString,
                orderFulfillmentTypeString
            )
        );

        // Derive the OrderFulfillment type hash using the corresponding type string.
        orderFulfillmentTypeHash = keccak256(orderFulfillmentTypeString);

        // Derive the OrderMetadata type hash using the corresponding type string.
        // @audit hookTypeString is missing
        orderMetadataTypeHash = keccak256(orderMetadataTypeString);
    }
}
```

> *If the struct type references other struct types (and these in turn reference even more struct types), then the set of referenced struct types is collected, sorted by name and appended to the encoding. An example encoding is `Transaction(Person from,Person to,Asset tx)Asset(address token,uint256 amount)Person(address wallet,string name)`. - from EIP 712 [(link](https://eips.ethereum.org/EIPS/eip-712#definition-of-encodetype))*
>

Also read this Stack Overflow page - [link](https://stackoverflow.com/questions/75019935/how-to-define-typehash-for-eip712-typed-data-signing-with-nested-struct-in-solid)

## Tools Used

Manual Review

## Recommended Mitigation Steps

Make sure to include the typestring for **`Hook`** when generating the typehash for **`OrderMetadata`**.

```diff
function _deriveRentalTypehashes()
    internal
    pure
    returns (
        bytes32 itemTypeHash,
        bytes32 hookTypeHash,
        bytes32 rentalOrderTypeHash,
        bytes32 orderFulfillmentTypeHash,
        bytes32 orderMetadataTypeHash,
        bytes32 rentPayloadTypeHash
    )
{
    // Construct the Item type string.
    bytes memory itemTypeString = abi.encodePacked(
        "Item(uint8 itemType,uint8 settleTo,address token,uint256 amount,uint256 identifier)"
    );

    // Construct the Hook type string.
    bytes memory hookTypeString = abi.encodePacked(
        "Hook(address target,uint256 itemIndex,bytes extraData)"
    );

    // Construct the RentalOrder type string.
    bytes memory rentalOrderTypeString = abi.encodePacked(
        "RentalOrder(bytes32 seaportOrderHash,Item[] items,Hook[] hooks,uint8 orderType,address lender,address renter,address rentalWallet,uint256 startTimestamp,uint256 endTimestamp)"
    );

    // Derive the Item type hash using the corresponding type string.
    itemTypeHash = keccak256(itemTypeString);

    // Derive the Hook type hash using the corresponding type string.
    hookTypeHash = keccak256(hookTypeString);

    // Derive the RentalOrder type hash using the corresponding type string.
    rentalOrderTypeHash = keccak256(
        abi.encode(rentalOrderTypeString, hookTypeString, itemTypeString)
    );

    {
        // Construct the OrderFulfillment type string.
        bytes memory orderFulfillmentTypeString = abi.encodePacked(
            "OrderFulfillment(address recipient)"
        );

        // Construct the OrderMetadata type string.
        bytes memory orderMetadataTypeString = abi.encodePacked(
            "OrderMetadata(uint8 orderType,uint256 rentDuration,Hook[] hooks,bytes emittedExtraData)"
        );

        // Construct the RentPayload type string.
        bytes memory rentPayloadTypeString = abi.encodePacked(
            "RentPayload(OrderFulfillment fulfillment,OrderMetadata metadata,uint256 expiration,address intendedFulfiller)"
        );

        // Derive RentPayload type hash via combination of relevant type strings.
        rentPayloadTypeHash = keccak256(
            abi.encodePacked(
                rentPayloadTypeString,
                orderMetadataTypeString,
                orderFulfillmentTypeString
            )
        );

        // Derive the OrderFulfillment type hash using the corresponding type string.
        orderFulfillmentTypeHash = keccak256(orderFulfillmentTypeString);

        // Derive the OrderMetadata type hash using the corresponding type string.
        // @audit hookTypeString is missing
-       orderMetadataTypeHash = keccak256(orderMetadataTypeString);
+       orderMetadataTypeHash = keccak256(abi.encode(orderMetadataTypeString, hookTypeString));
    }
}
```

# [M-02] NFTs can be freely burned by renter at low cost

## Impact

ERC721 and ERC1155 tokens that implement `[ERC721Burnable](https://github.com/OpenZeppelin/openzeppelin-contracts/blob/master/contracts/token/ERC721/extensions/ERC721Burnable.sol)` can be rented for a short rent period and maliciously burned because `burn` function is not disabled in `Guard::checkTransaction`.

[Guard.sol#L12-L31](https://github.com/re-nft/smart-contracts/blob/3ddd32455a849c3c6dc3c3aad7a33a6c9b44c291/src/policies/Guard.sol#L12C1-L31C45)

```solidity
import { //@audit burn selector is missing
    shared_set_approval_for_all_selector,
    e721_approve_selector,
    e721_safe_transfer_from_1_selector,
    e721_safe_transfer_from_2_selector,
    e721_transfer_from_selector,
    e721_approve_token_id_offset,
    e721_safe_transfer_from_1_token_id_offset,
    e721_safe_transfer_from_2_token_id_offset,
    e721_transfer_from_token_id_offset,
    e1155_safe_transfer_from_selector,
    e1155_safe_batch_transfer_from_selector,
    e1155_safe_transfer_from_token_id_offset,
    e1155_safe_batch_transfer_from_token_id_offset,
    gnosis_safe_set_guard_selector,
    gnosis_safe_enable_module_selector,
    gnosis_safe_disable_module_selector,
    gnosis_safe_enable_module_offset,
    gnosis_safe_disable_module_offset
} from "@src/libraries/RentalConstants.sol";
```

Burning will lead to loss of funds from lender as it will cost him the total price of the NFT +  amount from the rent because `Stop::stopRent()` will revert when trying to transfer back the token to the lender.
Renter will have to pay for the rent period, but there are `ERC721` tokens that return the floor price of the NFT when burned - https://medium.com/@PrimeNumbersFi/the-burn-to-redeem-function-14c45827525a

> Opensea allows NFT collections with a burn mechanism and most of the NFT users don’t have the appropriate knowledge given the fact that they are not technical users.
> 

There are some NFT collections that support token burning:

https://opensea.io/learn/nft/what-is-nft-burning

As we can see there are [NFTs](https://opensea.io/assets/ethereum/0x34eebee6942d8def3c125458d1a86e0a897fd6f9/4) with burn functionality and prices over $100k.

## Proof of Concept

1. Alice has an NFT from a valuable collection.
2. She sees that Bob (owner of another NFT from the same collection) lists his NFT for renting and sees the opportunity to increase the value of her own NFT by burning his. 
3. Fulfills the order and rents it for 1 day.
4. Effectively bypasses the `Guard::checkTransaction()` and burns the token.

Here is a test that demonstrates step 4, due to the off-chain nature of previous steps:

1. Navigate to `test/unit/Guard/CheckTransaction.sol`
2. **Add new storage variable:**

```solidity
bytes4 public e721_burn_selector = 0x42966c68;
```

1. Paste the test and execute it with the following command:

```bash
forge test --match-test test_Success_CheckTransaction_ERC721_Burn
```

```solidity
function test_Success_CheckTransaction_ERC721_Burn() public {
      // Create a rentalId array
      RentalAssetUpdate[] memory rentalAssets = new RentalAssetUpdate[](1);
      rentalAssets[0] = RentalAssetUpdate(RentalUtils.getItemPointer(address(alice.safe), address(erc721s[0]), 0), 1);

      // Mark the rental as actively rented in storage
      _markRentalsAsActive(rentalAssets); //@audit needed to mark the assets as rented which will trigger the checkTransaction execution
      bytes memory burnFromCalldata = abi.encodeWithSelector(bytes4(keccak256(bytes("burn(uint256)"))), 0); //@audit burn is not handled in checkTransaction allowing renters to burn tokens

      // Expect revert because of an unauthorized function selector
      _checkTransactionRevertUnauthorizedSelector(
          address(alice.safe), address(erc721s[0]), e721_burn_selector, burnFromCalldata
      );
  }
```

1. Expected outcome is: **[FAIL. Reason: call did not revert as expected]**, because burn selector is not specified in the `Guard` contract and tx passes successfully.

## Tools Used

Manual Review

## Recommended Mitigation Steps

Add burn selector to the `Guard` and disable this functionality too, there are only negatives of allowing such transactions.

[Guard.sol#L12-L31](https://github.com/re-nft/smart-contracts/blob/3ddd32455a849c3c6dc3c3aad7a33a6c9b44c291/src/policies/Guard.sol#L12C1-L31C45)

```diff
import {
    shared_set_approval_for_all_selector,
    e721_approve_selector,
    e721_safe_transfer_from_1_selector,
    e721_safe_transfer_from_2_selector,
    e721_transfer_from_selector,
    e721_approve_token_id_offset,
    e721_safe_transfer_from_1_token_id_offset,
    e721_safe_transfer_from_2_token_id_offset,
    e721_transfer_from_token_id_offset,
+   e721_burn_selector,
+   e1155_burn_selector,
+   e1155_burn_batch_selector,
    e1155_safe_transfer_from_selector,
    e1155_safe_batch_transfer_from_selector,
    e1155_safe_transfer_from_token_id_offset,
    e1155_safe_batch_transfer_from_token_id_offset,
    gnosis_safe_set_guard_selector,
    gnosis_safe_enable_module_selector,
    gnosis_safe_disable_module_selector,
    gnosis_safe_enable_module_offset,
    gnosis_safe_disable_module_offset
} from "@src/libraries/RentalConstants.sol";
```

[RentalConstants.sol](https://github.com/re-nft/smart-contracts/blob/3ddd32455a849c3c6dc3c3aad7a33a6c9b44c291/src/libraries/RentalConstants.sol)

```diff
+ // bytes4(keccak256("burn(uint256)"));
+ bytes4 constant e721_burn_selector = 0x42966c68;
+ uint256 constant e721_burn_token_id_offset = 0x24;

+ // bytes4(keccak256("burn(address,uint256,uint256)"));
+ bytes4 constant e1155_burn_selector = 0xf5298aca;
+ uint256 constant e1155_burn_token_id_offset = 0x24;

+ // bytes4(keccak256("burnBatch(address,uint256[],uint256[])"));
+ bytes4 constant e1155_burn_batch_selector = 0x6b20c454;
```

[Guard.sol::_checkTransaction()](https://github.com/re-nft/smart-contracts/blob/3ddd32455a849c3c6dc3c3aad7a33a6c9b44c291/src/policies/Guard.sol#L195-L293) add following code:

```solidity
else if (selector == e721_burn_selector) {
      // Load the token ID from calldata.
      uint256 tokenId = uint256(
          _loadValueFromCalldata(data, e721_burn_token_id_offset)
      );

      // Check if the selector is allowed.
      _revertSelectorOnActiveRental(selector, from, to, tokenId);
		}
else if (selector == e1155_burn_selector) {
      // Load the token ID from calldata.
      uint256 tokenId = uint256(
          _loadValueFromCalldata(data, e1155_burn_token_id_offset)
      );

      // Check if the selector is allowed.
      _revertSelectorOnActiveRental(selector, from, to, tokenId);
		}
else {
			if (selector == e1155_burn_batch_selector) {
					revert Errors.GuardPolicy_UnauthorizedSelector(e1155_burn_batch_selector);
			}
  }
```

# [M-03] Long period self rents will block hook disabling

## Impact

The protocol has admin-owned hooks, which can be used instead of `_checkTransaction` from GnosisSafe to disable transferring and approving the rented assets. There can be cases where hooks can contain vulnerable code in their `onTransaction` function and admins should disable them. Still, a malicious user can effectively block them from disabling by creating a rent to himself with a long rent period. As there will most likely be other rents using these hooks, admins will be forced to call `updateHookStatus` for `onStop` and all funds will be locked inside the `PaymentEscrow` contract, with no way to be recovered.

## Proof of Concept

[Stop.sol#L211](https://github.com/re-nft/smart-contracts/blob/main/src/policies/Stop.sol#L194-L212)

```solidity
function _removeHooks(
    Hook[] calldata hooks,
    Item[] calldata rentalItems,
    address rentalWallet
) internal {
    // Define hook target, item index, and item.
    address target;
    uint256 itemIndex;
    Item memory item;

    // Loop through each hook in the payload.
    for (uint256 i = 0; i < hooks.length; ++i) {
        // Get the hook address.
        target = hooks[i].target;

        // Check that the hook is reNFT-approved to execute on rental stop.
        if (!STORE.hookOnStop(target)) { 
-->         revert Errors.Shared_DisabledHook(target); //@audit will revert if hook onStop function is disabled
        }
```

## Tools Used

Manual Review

## Recommended Mitigation Steps

Consider adding a blacklist to disable hooks only for certain renters, this will ensure that other users who are using the **same hook will be able to stop their rents**, without losing their funds.

# [M-04] encodedData argument of OrderMetadata hashStruct is missing struct members which is not EIP712 compliant

## Impact

The encoding of OrderMetadata `encodedData` is inaccurately performed, leading to issues in verifying EIP712 signed messages.

`Signer::_deriveOrderMetadataHash()` is used in `Create::_isValidOrderMetadata()` and `Signer::_deriveOrderMetadataHash()` to check if the hash of the `OrderMetadata` struct is valid. However, there's a mistake in the encoding process because it doesn't hash all the members of **`OrderMetadata`**.

```solidity
function _deriveOrderMetadataHash(
    OrderMetadata memory metadata
) internal view returns (bytes32) {
    // Create array for hooks.
    bytes32[] memory hookHashes = new bytes32[](metadata.hooks.length);

    // Iterate over each hook.
    for (uint256 i = 0; i < metadata.hooks.length; ++i) {
        // Hash the hook
        hookHashes[i] = _deriveHookHash(metadata.hooks[i]);
    }

    // Derive and return the metadata hash as specified by EIP-712.
    // @audit why not pass 'orderType' and 'emittedExtraData'
@>  return
        keccak256(
            abi.encode(
                _ORDER_METADATA_TYPEHASH,
                metadata.rentDuration,
                keccak256(abi.encodePacked(hookHashes))
            )
        );
}
```

As you can see the `encodedData` includes only `OrderMetadata.rentDuration` and `OrderMetadata.hooks`, while `OrderMetadata.orderType` and `OrderMetadata.emittedExtraData` are not included.

According to EIP712, all struct members should be hashed in the order that they are defined.

> The encoding of a struct instance is `enc(value₁) ‖ enc(value₂) ‖ … ‖ enc(valueₙ)`, i.e. the concatenation of the encoded member values in the order that they appear in the type. Each encoded member value is exactly 32-byte long.
> 

Read More [here](https://eips.ethereum.org/EIPS/eip-712#definition-of-encodedata)

All of this will result in that only wrong hashed OrderMetadata though Signer::_deriveOrderMetadataHash() to pass when Seaport call validateOrder(). If the Lender pass correctly composed hash in `Seaport::OrderComponents::zoneHash` by himself and do not use `Create::getOrderMetadataHash()` for it, any rental will revert because in `Create::_isValidOrderMetadata()`.
This situation leads to an issue where only an incorrectly hashed `OrderMetadata` can pass through `Signer::_deriveOrderMetadataHash()` during the Seaport `validateOrder()`. Even if the Lender provides the correct hash in `Seaport::OrderComponents::zoneHash`, any rental will still revert in `Create::_isValidOrderMetadata()`.

## Proof of Concept

In the tests, when the lender provides the `zoneHash`, it relies on the incorrect `_deriveOrderMetadataHash()`. As a result, all these tests will pass, even though the underlying issue exists.

For accurate testing, use the correct OrderMetadata hash. This hash is generated in Remix using the fixed function outlined in the Recommendations section.

Hash: `0x04d9e28e69367406d7fdf7aaeb155428b0555405dbdf6521bfc87f6d3d19867c`

Insert it into the **`test/fixture/engine/OrderCreator.sol`** file at line 280 exactly as it is shown.

```solidity
.withZoneHash(0x04d9e28e69367406d7fdf7aaeb155428b0555405dbdf6521bfc87f6d3d19867c)
```

Run the basic test in `Rent::test_Success_Rent_BaseOrder_ERC721`. The test will revert `CreatePolicy_InvalidOrderMetadataHash`.

Run with: `forge test --match-test "test_Success_Rent_BaseOrder_ERC721" -vvvv`

![Untitled](https://i.imgur.com/zK77OBx.png)

> *Note: The `Reason: log != expected log` message is a result of some `expectEmit()` calls, but it doesn't affect the test outcome.*
> 

## Tools Used

Manual Review

## Recommended Mitigation Steps

Add `orderType` and `emittedExtraData` when create the `hashStruct`.

```diff
function _deriveOrderMetadataHash(
    OrderMetadata memory metadata
) internal view returns (bytes32) {
    // Create array for hooks.
    bytes32[] memory hookHashes = new bytes32[](metadata.hooks.length);

    // Iterate over each hook.
    for (uint256 i = 0; i < metadata.hooks.length; ++i) {
        // Hash the hook
        hookHashes[i] = _deriveHookHash(metadata.hooks[i]);
    }

    // Derive and return the metadata hash as specified by EIP-712.
    // @audit why not pass 'orderType' and 'emittedExtraData'
    return
        keccak256(
            abi.encode(
                _ORDER_METADATA_TYPEHASH,
+								metadata.orderType,
	              metadata.rentDuration,
                keccak256(abi.encodePacked(hookHashes)),
+								metadata.emittedExtraData
            )
        );
}
```

# [M-05] ERC721Pausable tokens can block reclaiming

**Issue Description:**

Since [ERC721Pausable](https://github.com/OpenZeppelin/openzeppelin-contracts/blob/master/contracts/token/ERC721/extensions/ERC721Pausable.sol) is an extension to the EIP712 standard, there will be many tokens that are going to implement it. 

One possible use case of renting an NFTs will be to show proof of an ownership to an specific game or organisation, for example Bored Ape or Axie Infinity game token. 

If pausable token is being renter there is nothing to stop the renter from **pausing the transfers of these NFTs**.

He will be able to continue to use it, while the rent will be permanent and there will be no way to recover the rented items from the `Stop` module.

**Recommendation:**

Consider adding `pause`/`unpause` selectors to the `Guard::checkTransaction`, that will successfully mitigate the problem.

# [L-01] CryptoPunk NFTs will be stolen

**Issue Description:**

CryptoPunks collection was created before EIP712 was introduced and has an additional function used to offer punk for sale to certain addresses:
`offerPunkForSaleToAddress`. 

```solidity
function offerPunkForSaleToAddress(uint punkIndex, uint minSalePriceInWei, address toAddress) {
    if (!allPunksAssigned) throw;
    if (punkIndexToAddress[punkIndex] != msg.sender) throw;
    if (punkIndex >= 10000) throw;
    punksOfferedForSale[punkIndex] = Offer(true, punkIndex, msg.sender, minSalePriceInWei, toAddress);
    PunkOffered(punkIndex, minSalePriceInWei, toAddress);
}
```

CryptoPunk NFT renter can call freely this function because Guard is not handling it and pass 1 wei as a price.

Then after rental period is terminated he will still have approval to buy it without any interruptions.

**Recommendation:**

There is no universal mitigation for all these non-standard NFT implementations but care must be taken when dealing with such tokens and especially CryptoPunks as they are the foundation of the whole NFT ecosystem.

# [L-02] Some NFTs will return the floor price when burned

**Issue Description:**

There are certain NFTs that will return the floor price to the owner when burned.
One example is the [Burnables](https://opensea.io/collection/burnables-nft?embed%5B0%5D=test&embed%5B1%5D=test(.(%22()%27.%2C%2C). 
We can verify this statement from the description of the collection:

> The first-ever refundable NFT. The owner of the token can burn their NFT at any time and have the original mint price sent to their wallet.

Users who have such a token and are non-technical can also rent their NFTs. Then renter who spotted it can rent it for short period and burn it to receive the **mint price** of the `NFT`.

This will result in a direct profit for renter, and big loss for the lender because rent payment won’t be distributed as well.

**Recommendation:**

Add burn function selector which will remove all the malicious use cases for such tokens.

## [L-03] No validation if `Order.orderType` and `OrderMetadata.orderType` are equal

**Issue Description:**

The `OrderMetadata` hash, generated in `_deriveOrderMetadataHash()`, is missing two crucial members: `orderType` and `emittedExtraData`. When fulfilling an order, Seaport calls `Create::validateOrder()`, performing necessary logic on zones, conversion, and checks. The problem arises because the Lender sets `Order.orderType` initially, but the Renter can later provide a different value in `OrderMetadata`. As the hash doesn't consider `orderType`, `_isValidOrderMetadata()` passes, causing items to be converted based on the Renter's `OrderMetadata.orderType`, leading to potential inconsistency. For example, if the Lender sets `PAY` as the `Order.orderType`, the Renter can pass `BASE` in `OrderMetadata`, resulting in inconsistency.

As all `PAY` orders are provided with `PAYEE` by the reNFT backend and executed collectively, the second `validateOrder()` call in `_processBaseOrderConsideration()` reverts, because it will try to convert ERC721, but the function only work with ERC20. However, this scenario has no impact whatsoever, except for the pre-existing inconsistency.

**Recommendation:**

To avoid the inconsistency, consider checking whether `Order.orderType` equals `OrderMetadata.orderType` initially in `validateOrder()`. This step can help skip unnecessary computations or align all logic based on `Order.orderType` rather than relying on the fulfiller's `OrderMetadata.orderType`.