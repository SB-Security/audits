## Findings Summary

| ID | Description | Severity |
| - | - | - |
| [H-01](#h-01-univ3connector_getpositiontvl-reads-information-for-the-whole-nonfungiblepositionmanager-instead-of-the-position-of-tokenid) |`UniV3Connector::_getPositionTVL` reads information for the whole NonFungiblePositionManager instead of the position of tokenId | High |
| [H-02](#h-02-accountingmanager-blacklisted-receiver-can-block-the-entire-batch) |AccountingManager blacklisted receiver can block the entire batch | High |
| [H-03](#h-03-balancerconnector_getpositiontvl-is-calculated-incorrectly) |`BalancerConnector::_getPositionTVL` is calculated incorrectly | High |
| [M-01](#m-01-_getpositiontvl-calculations-relying-on-totalsupply-are-sandwichable) |`_getPositionTVL` calculations relying on `totalSupply` are sandwichable | Medium |
| [M-02](#m-02-accountingmanager---lack-of-slippage-on-depositwithdraw) |AccountingManager - Lack of slippage on deposit/withdraw | Medium |
| [M-03](#m-03-keeper-does-not-implement-eip712-correctly-on-multiple-occasions) |Keeper does not implement `EIP712` correctly on multiple occasions | Medium |
| [M-04](#m-04-balancerflashloan-can-be-full-dos-with-1-wei-donation) |BalancerFlashLoan can be full DoS with 1 wei donation  | Medium |
| [M-05](#m-05-noyafeereceiver-should-not-start-the-withdraw-flow-for-fees) |`NoyaFeeReceiver` should not start the withdraw flow for fees | Medium |


## [H-01] `UniV3Connector::_getPositionTVL` reads information for the whole NonFungiblePositionManager instead of the position of tokenId

## Impact

`UNIv3Connector::_getPositionTVL` will return information about all the positions in `NonFungiblePositionManager` that have provided liquidity in same tick range, not only the position with `tokenId` minted through Noya.

## Proof of Concept

In `_getPositionTVL` we can see that the `liquidity`, `tokensOwed0`, and `tokensOwed1` are being directly fetched from the pool. This approach is wrong because it gets the information about all the positions that `NonFungiblePositionManager` manages in these ticks and not only the positions opened through `UNIv3Connector::openPosition`  for given `tokenId`:

[UNIv3Connector.sol#L127-L150](https://github.com/code-423n4/2024-04-noya/blob/main/contracts/connectors/UNIv3Connector.sol#L127-L150)

```solidity
function _getPositionTVL(HoldingPI memory p, address base) public view override returns (uint256 tvl) {
    PositionBP memory positionInfo = registry.getPositionBP(vaultId, p.positionId);
    uint256 tokenId = abi.decode(p.data, (uint256));
    (address token0, address token1) = abi.decode(positionInfo.data, (address, address));
    uint256 amount0;
    uint256 amount1;
    (int24 tL, int24 tU, uint24 fee) = abi.decode(p.additionalData, (int24, int24, uint24));
    {
        IUniswapV3Pool pool = IUniswapV3Pool(factory.getPool(token0, token1, fee));
        bytes32 key = keccak256(abi.encodePacked(positionManager, tL, tU));

        (uint128 liquidity,,, uint128 tokensOwed0, uint128 tokensOwed1) = pool.positions(key);//@audit wrong
...MORE CODE
}
```

If we look carefully in order to calculate the **position key**, the address of the `NonFungiblePositionManager` (positionManager) is being used and since it manages positions in pools on behalf of users, the positions will hold liquidity of all `NonfungiblePositionManager`'s `NFTs` - it won’t be able to differentiate the Noya positions.

As a result, `liquidity`, `tokensOwed0`, `tokensOwed1` will be highly increased because they will contain information about other positions as well. This will result in a artificially increased `TVL`, which is used in the following functions:

- `AccountingManager::getProfit` - will distribute a lot more performance fee than needed and can impact the checks in `checkIfTVLHasDroped`.
- `AccountingManager::deposit` - most likely will block the deposits
- `OmnichainManagerNormalChain::updateTVLInfo` - artificially increase the TVL on destination chain.

### POC

https://gist.github.com/AydoanB/817310bc276121faa287f4269027311b

Should be placed in `testFoundry/UNIv3Connector.sol` and executed with the following command:

```solidity
forge test --match-contract "testUNIv3" --match-test "testOpenPosition" -vv
```

> Make sure to import the proper interfaces.
> 

The test verifies that positions created outside of `Noya` in the same tick ranges will be accounted when evaluating the `TVL` and in particular `_getPositionTVL` of `UNIv3Connector` position, furthermore to validate that `createAndMintPosOutsideNoya` increases the TVL, test can be executed twice after excluding the function.

```solidity
------------- Wrong way (all positions in this NFPM for this tick) - currently used in Noya --------
tokensOwed0Pool 0
tokensOwed1Pool 0
liquidityPool 73736819811566
------------------------------------------------------------
TVL: 30180026
------------- Right way (Noya position liquidity for this tick) - proper way to get position data --------
tokensOwed0 0
tokensOwed1 0
liquidity 36868409905783
------------------------------------------------------------
```

## Tools Used

Manual Review

## Recommended Mitigation Steps

Use `NonFungiblePositionManager::positions`  for the `tokenId`  that has been saved in the `positionData` when minting position in `UNIv3Connector::openPosition`. Consider whether the fees should be claimed first, otherwise you risk to not receive up-to-date values.

## [H-02] AccountingManager blacklisted receiver can block the entire batch

## Impact

Vaults that use tokens with [blacklist mechanism](https://github.com/d-xo/weird-erc20?tab=readme-ov-file#tokens-with-blocklists) will have their entire withdrawal functionality blocked when someone passes such receiver.

## Proof of Concept

`AccountingManager::withdraw` allows passing a receiver who has to receive the tokens at the `executionWithdraw`. The problem is that `withdraw` can be called even without having a single share.

[AccountingManager.sol#L304-L316](https://github.com/code-423n4/2024-04-noya/blob/main/contracts/accountingManager/AccountingManager.sol#L304-L316)

```solidity
function withdraw(uint256 share, address receiver) public nonReentrant whenNotPaused {
    if (balanceOf(msg.sender) < share + withdrawRequestsByAddress[msg.sender]) {
        revert NoyaAccounting_INSUFFICIENT_FUNDS(
            balanceOf(msg.sender), share, withdrawRequestsByAddress[msg.sender]
        );
    }
    withdrawRequestsByAddress[msg.sender] += share;

    // adding the withdraw request to the withdraw queue
    withdrawQueue.queue[withdrawQueue.last] = WithdrawRequest(msg.sender, receiver, block.timestamp, 0, share, 0);
    emit RecordWithdraw(withdrawQueue.last, msg.sender, receiver, share, block.timestamp);
    withdrawQueue.last += 1;
} 
```

By passing 0 to share argument and **blacklisted** user to receiver - if check will be completely bypassed from everyone, even if his balance is 0 and request will be created.

The consequences will be a complete DoS for the withdraw functionality because `currentWithdrawGroup` will not be able to deleted in `executeWithdraw` since not all indexes in the `withdrawQueue` will be iterated:

[AccountingManager.sol#L396-L446](https://github.com/code-423n4/2024-04-noya/blob/main/contracts/accountingManager/AccountingManager.sol#L396-L446)

```solidity
function executeWithdraw(uint256 maxIterations) public onlyManager nonReentrant whenNotPaused {
...MORE CODE
    // if the withdraw group is fullfilled and there are no withdraws that are waiting for execution, we delete the withdraw group
    if (currentWithdrawGroup.lastId == firstTemp) {
        delete currentWithdrawGroup;
    }
}
```

`isFulfilled` and `isStarted` will remain true and will prevent the system from starting new withdraw group:

[AccountingManager.sol#L360-L365](https://github.com/code-423n4/2024-04-noya/blob/main/contracts/accountingManager/AccountingManager.sol#L360-L365)

```solidity
  function startCurrentWithdrawGroup() public onlyManager nonReentrant whenNotPaused {
      require(currentWithdrawGroup.isStarted == false && currentWithdrawGroup.isFullfilled == false); //@audit in this scenarion both will be true
      currentWithdrawGroup.isStarted = true;
      currentWithdrawGroup.lastId = withdrawQueue.middle;
      emit WithdrawGroupStarted(currentWithdrawGroup.lastId, currentWithdrawGroup.totalCBAmount);
  }
```

## Tools Used

Manual Review

## Recommended Mitigation Steps

Make it possible to move the middle `firstTemp`, which is used in the execute step, in both directions, not only back.

## [H-03] `BalancerConnector::_getPositionTVL` is calculated incorrectly

## Impact

BalancerConnector misuses the weight of pool tokens, leading to decreased TVL of these positions.

## Proof of Concept

If we take a look at the balancer documentation and especially how the tokens are being valued, we can see that they are not using weight to determine the price of the `pool.tokens[pool.tokenIndex]`:

[BalancerDocs](https://docs.balancer.fi/reference/lp-tokens/valuing.html#directly-calculating-nav)

```solidity
(tokens, balances, lastChangeBlock) = vault.getPoolTokens(poolId);
prices = fetchPricesFromPriceProvider(tokens); //ex. CoinGecko
poolValueUsd = sum(balances[i]*price[i]);
bptPriceUsd = poolValueUsd/bpt.totalSupply();
```

As we can see they are directly calculate the balance by the price, which is not the case in `Noya`:

[BalancerConnector.sol#L162-L173](https://github.com/code-423n4/2024-04-noya/blob/main/contracts/connectors/BalancerConnector.sol#L162-L173)

```solidity
function _getPositionTVL(HoldingPI memory p, address base) public view override returns (uint256) {
    PositionBP memory PTI = registry.getPositionBP(vaultId, p.positionId);
    PoolInfo memory pool = abi.decode(PTI.additionalData, (PoolInfo));
    uint256 lpBalance = totalLpBalanceOf(pool);
    (, uint256[] memory _tokenBalances,) = IBalancerVault(balancerVault).getPoolTokens(pool.poolId);
    uint256 _totalSupply = IERC20(pool.pool).totalSupply();

    uint256 _weight = pool.weights[pool.tokenIndex];

    uint256 token1bal = valueOracle.getValue(pool.tokens[pool.tokenIndex], base, _tokenBalances[pool.tokenIndex]);
    return (((1e18 * token1bal * lpBalance) / _weight) / _totalSupply);
} 
```

The reason why weight is not needed here is because the balances returned from `getPoolTokens` are already split by them. If we look at the DAI-USDC-USDT pool we will see that balances returned are already splitted and there is no need to be divided by their weights.

![Image](https://i.imgur.com/F6CqZlt.png)

But since Noya uses weights to divide the balance of the tokens, the result will be amount which is percentage weight, set in the `pool.weights[pool.tokenIndex]`, of the actual balance and TVL will be accounted much lower as it is in reality.

Example with USDT and values from [`BalancerConnector.t.sol`](https://github.com/code-423n4/2024-04-noya/blob/main/testFoundry/BalancerConnector.t.sol#L96-L125):

`return (((1e18 * 423099282190 * 991435857075096399976) / 333333333333333333) / 2596148431740844293012911818494888);`**≈ 0.484**

## Tools Used

Manual Review

## Recommended Mitigation Steps

Do not use weights to evaluate the TVL of the position, instead just refactor the `_getPositionTVL`'s return with proper decimal scaling based on the asset used.


## [M-01] `_getPositionTVL` calculations relying on `totalSupply` are sandwichable

## Impact

The following connectors:

- AerodromeConnector
- BalancerConnector
- CamelotConnector

Rely on `totalSupply` of provided assets to calculate the share of the connector which makes it possible attacker to manipulate the  `TVLHelper::getTVL`.

## Proof of Concept

Relying on external supply to calculate the `TVL` of Noya is easy to manipulate. 

Consider the example with `CamelotConnector` (the same applies to the others):

[CamelotConnector.sol#L88-L97](https://github.com/code-423n4/2024-04-noya/blob/main/contracts/connectors/CamelotConnector.sol#L88-L97)

```solidity
 function _getPositionTVL(HoldingPI memory p, address base) public view override returns (uint256 tvl) {
    (address tokenA, address tokenB) =
        abi.decode(registry.getPositionBP(vaultId, p.positionId).data, (address, address));
    address pool = factory.getPair(tokenA, tokenB);
    uint256 totalSupply = IERC20(pool).totalSupply();
    (uint256 reserves0, uint256 reserves1,,) = ICamelotPair(pool).getReserves();

    uint256 balanceThis = IERC20(pool).balanceOf(address(this));
    return balanceThis * (_getValue(tokenA, base, reserves0) + _getValue(tokenB, base, reserves1)) / totalSupply;
}
```

`totalSupply` is used to get the balance that this connector has in the Camelot pool- when it increases the balance is decreased and vice-versa.

Attacker can use this to add liquidity in the same pool and increase the `totalSupply` in CamelotPair:

[CamelotPair.sol#L164-L189](https://github.com/CamelotLabs/core/blob/0fa055d19c4ef9f34a29a940c90527e2840edd3d/contracts/CamelotPair.sol#L183) (external contract)

```solidity
function mint(address to) external lock returns (uint liquidity) {
  (uint112 _reserve0, uint112 _reserve1,,) = getReserves();
  // gas savings
  uint balance0 = IERC20(token0).balanceOf(address(this));
  uint balance1 = IERC20(token1).balanceOf(address(this));
  uint amount0 = balance0.sub(_reserve0);
  uint amount1 = balance1.sub(_reserve1);

  bool feeOn = _mintFee(_reserve0, _reserve1);
  uint _totalSupply = totalSupply;
  // gas savings, must be defined here since totalSupply can update in _mintFee
  if (_totalSupply == 0) {
    liquidity = Math.sqrt(amount0.mul(amount1)).sub(MINIMUM_LIQUIDITY);
    _mint(address(0), MINIMUM_LIQUIDITY);
    // permanently lock the first MINIMUM_LIQUIDITY tokens
  } else {
    liquidity = Math.min(amount0.mul(_totalSupply) / _reserve0, amount1.mul(_totalSupply) / _reserve1);
  }
  require(liquidity > 0, 'CamelotPair: INSUFFICIENT_LIQUIDITY_MINTED');
  _mint(to, liquidity);//increases totalSupply

  _update(balance0, balance1);
  if (feeOn) kLast = _k(uint(reserve0), uint(reserve1));
  // reserve0 and reserve1 are up-to-date
  emit Mint(msg.sender, amount0, amount1);
}
```

From here he can create the following attack:

1. FlashLoan tokenA and tokenB
2. add liquidity in this particular pair in which `CamelotConnector` has provided as well
3. Mint enormous amount of LP tokens, increasing the `totalSupply` exponentially, resulting in a direct manipulation of the `TVL` and in particular `_getPositionTVL` of all the `CamelotConnectors`

```solidity
function _getPositionTVL(HoldingPI memory p, address base) public view override returns (uint256 tvl) {
...MORE CODE
      uint256 totalSupply = IERC20(pool).totalSupply();
      (uint256 reserves0, uint256 reserves1,,) = ICamelotPair(pool).getReserves();

      uint256 balanceThis = IERC20(pool).balanceOf(address(this));
      return balanceThis * (_getValue(tokenA, base, reserves0) + _getValue(tokenB, base, reserves1)) / totalSupply;
  }
```

1. Call `AccountingManager::checkIfTVLHasDroped` , returning decreased `recordProfitForFee` because of the increased `totalSupply`

[AccountingManager.sol#582-L588](https://github.com/code-423n4/2024-04-noya/blob/main/contracts/accountingManager/AccountingManager.sol#L582-L588)

```solidity
function getProfit() public view returns (uint256) {
        uint256 tvl = TVL();
        if (tvl + totalWithdrawnAmount > totalDepositedAmount) {
            return tvl + totalWithdrawnAmount - totalDepositedAmount;
        }
        return 0;
    }
```

[AccountingManager.sol#L493-L500](https://github.com/code-423n4/2024-04-noya/blob/main/contracts/accountingManager/AccountingManager.sol#L493-L500)

```solidity
 function checkIfTVLHasDroped() public nonReentrant {
        uint256 currentProfit = getProfit();
        if (currentProfit < storedProfitForFee) {
            emit ResetFee(currentProfit, storedProfitForFee, block.timestamp);
            preformanceFeeSharesWaitingForDistribution = 0;
            profitStoredTime = 0;
        }
    }
```

1. Wipe the `performance fee`, making the `performanceFeeReceiver` wait additional 12 hours to be able to claim it.
2. Remove liquidity and pay back the flash loans, as well as returning the `_getPositionTVL` calculation to normal.

## Tools Used

Manual Review

## Recommended Mitigation Steps

Consider rewriting the `_getPositionTVL` functions to not rely on spot values and in particular `totalSupply`.

## [M-02] AccountingManager - Lack of slippage on deposit/withdraw

## Impact

`AccountingManager::deposit` and `AccountingManager::withdraw` have no slippage protection. Swaps inside rely on Chainlink oracles or Uniswap TWAPs, which means there will be price changes on any of request, calculation or execution step. The fact that deposits and withdrawals are multi-step operations increases the impact.

## Proof of Concept

As you can see, none of the functions have a slippage parameter that allows the user to specify a minimum amount they expect to receive. This means that the user has no control over how much shares or base tokens they will receive in return.

[AccountingManager.sol#L200](https://github.com/code-423n4/2024-04-noya/blob/9c79b332eff82011dcfa1e8fd51bad805159d758/contracts/accountingManager/AccountingManager.sol#L200)

```solidity
function deposit(address receiver, uint256 amount, address referrer) public nonReentrant whenNotPaused {
```

[AccountingManager.sol#L304](https://github.com/code-423n4/2024-04-noya/blob/9c79b332eff82011dcfa1e8fd51bad805159d758/contracts/accountingManager/AccountingManager.sol#L304)

```solidity
function withdraw(uint256 share, address receiver) public nonReentrant whenNotPaused {
```

## Tools Used

Manual Review

## Recommended Mitigation Steps

Add `minAmountOut` to `deposit()` and `minAmountOut` to `withdraw()`.

## [M-03] Keeper does not implement EIP712 correctly on multiple occasions

## Impact

Keeper will not be EIP712 compliant which lead to issues with function executions.

## Proof of Concept

### 1. `TXTYPE_HASH` EncodeType is incorrect

`TXTYPE_HASH` needs to be `name ‖ "(" ‖ member₁ ‖ "," ‖ member₂ ‖ "," ‖ … ‖ memberₙ ")”` as specified in EIP712 **-** https://eips.ethereum.org/EIPS/eip-712#definition-of-encodetype

But now there is an extra space after the `address executor`, resulting in a different hash:

[Keepers.sol#L12](https://github.com/code-423n4/2024-04-noya/blob/9c79b332eff82011dcfa1e8fd51bad805159d758/contracts/governance/Keepers.sol#L12)

```solidity
bytes32 public constant TXTYPE_HASH = keccak256(
    "Execute(uint256 nonce,address destination,bytes data,uint256 gasLimit,address executor, uint256 deadline)"
);
```

![Screenshot](https://i.imgur.com/K05H7Ad.png)

### 2. `TXTYPE_HASH` EncodedData is incorrect

The **TXTYPE_HASH** `data` parameter is of type `bytes`, but is not hashed when calculating the hash.

As specified in EIP712 - https://eips.ethereum.org/EIPS/eip-712#definition-of-encodedata:

> *The dynamic values `bytes` and `string` are encoded as a `keccak256` hash of their contents.*
> 

[Keepers.sol#L101](https://github.com/code-423n4/2024-04-noya/blob/9c79b332eff82011dcfa1e8fd51bad805159d758/contracts/governance/Keepers.sol#L101)

```solidity
bytes32 txInputHash = keccak256(abi.encode(TXTYPE_HASH, nonce, destination, data, gasLimit, executor, deadline));
```

## Tools Used

Manual Review

## Recommended Mitigation Steps

### Mitigation of Problem 1:

```diff
- bytes32 public constant TXTYPE_HASH = keccak256(
-    "Execute(uint256 nonce,address destination,bytes data,uint256 gasLimit,address executor, uint256 deadline)"
- );

+ bytes32 public constant TXTYPE_HASH = keccak256(
+    "Execute(uint256 nonce,address destination,bytes data,uint256 gasLimit,address executor,uint256 deadline)"
+ );
```

### Mitigation of Problem 2:

```diff
- bytes32 txInputHash = keccak256(abi.encode(TXTYPE_HASH, nonce, destination, data, gasLimit, executor, deadline));
+ bytes32 txInputHash = keccak256(abi.encode(TXTYPE_HASH, nonce, destination, keccak256(data), gasLimit, executor, deadline));
```

## [M-04] BalancerFlashLoan can be full DoS with 1 wei donation

## Impact

`BalancerFlashLoan` can be completely blocked from functioning if someone donates 1 wei from the needed token.

## Proof of Concept

Looking at the `BalancerFlashLoan::receiveFlashLoan` we can see the following check:

```solidity
  require(tokens[i].balanceOf(address(this)) == 0, "BalancerFlashLoan: Flash loan extra tokens");
```

[BalancerFlashLoan.sol#L54-L94](https://github.com/code-423n4/2024-04-noya/blob/9c79b332eff82011dcfa1e8fd51bad805159d758/contracts/connectors/BalancerFlashLoan.sol#L54-L94)

```solidity
54  function receiveFlashLoan(
55      IERC20[] memory tokens,
56      uint256[] memory amounts,
57      uint256[] memory feeAmounts,
58      bytes memory userData
59  ) external override {
60      emit ReceiveFlashLoan(tokens, amounts, feeAmounts, userData);
61      require(msg.sender == address(vault));
62      (
63          uint256 vaultId,
64          address receiver,
65          address[] memory destinationConnector,
66          bytes[] memory callingData,
67          uint256[] memory gas
68      ) = abi.decode(userData, (uint256, address, address[], bytes[], uint256[]));
69      (,,, address keeperContract,, address emergencyManager) = registry.getGovernanceAddresses(vaultId);
70      if (!(caller == keeperContract)) {
71          revert Unauthorized(caller);
72      }
73      if (registry.isAnActiveConnector(vaultId, receiver)) {
74          for (uint256 i = 0; i < tokens.length; i++) {
75              // send the tokens to the receiver
76              tokens[i].safeTransfer(receiver, amounts[i]);
77              amounts[i] = amounts[i] + feeAmounts[i];
78          }
79          for (uint256 i = 0; i < destinationConnector.length; i++) {
80              // execute the transactions
81              (bool success,) = destinationConnector[i].call{ value: 0, gas: gas[i] }(callingData[i]);
82              require(success, "BalancerFlashLoan: Flash loan failed");
83          }
84          for (uint256 i = 0; i < tokens.length; i++) {
85              // send the tokens back to this contract
86              BaseConnector(receiver).sendTokensToTrustedAddress(address(tokens[i]), amounts[i], address(this), "");
87          }
88      }
89      for (uint256 i = 0; i < tokens.length; i++) {
90          // send the tokens back to the vault
91          tokens[i].safeTransfer(msg.sender, amounts[i]);
92          require(tokens[i].balanceOf(address(this)) == 0, "BalancerFlashLoan: Flash loan extra tokens");
93      }
94  }

```

It is important to mention that on `line 91` only amount that was flash loaned + the fees will be transferred to Balancer, any additional contract balance will be left.

The assumption that the balance of the tokens will always be 0 in `line 92` is wrong because no transfer was called in `BalancerFlashLoan::makeFlashLoan` to empty the balances.

**Attackers knowing for which tokens the flashLoan contract will be used, since it is tied to particular `vault`, can send 1 wei from either of the tokens and DoS the entire contract.** 

> There is no way to unstuck the contract, since it does not inherits from `BaseConnector` and it has no `sendTokensToTrustedAddress` to transfer the funds.
> 

## Tools Used

Manual Review

## Recommended Mitigation Steps

In the beginning of `makeFlashLoan` you can foreach the tokens and withdraw all the non-zero balances of the address.

```solidity
  function makeFlashLoan(IERC20[] memory tokens, uint256[] memory amounts, bytes memory userData)
      external
      nonReentrant
  {
      caller = msg.sender;
      emit MakeFlashLoan(tokens, amounts);
      vault.flashLoan(this, tokens, amounts, userData);
      caller = address(0);
  }
```

## [M-05] NoyaFeeReceiver should not start the withdraw flow for fees

## Impact

`NoyaFeeReceiver` cannot claim all withdraw fees, because the fees will also start the withdraw flow, and in the end will calculate fee over the fees.

## Proof of Concept

For each withdrawal request that will be executed via `executeWithdraw()`, there will be a withdrawal fee deducted.

[AccountingManager.sol#L421-L440](https://github.com/code-423n4/2024-04-noya/blob/9c79b332eff82011dcfa1e8fd51bad805159d758/contracts/accountingManager/AccountingManager.sol#L421-L440)

```solidity
function executeWithdraw(uint256 maxIterations) public onlyManager nonReentrant whenNotPaused {
   ...

        processedBaseTokenAmount += data.amount;
        {
            uint256 feeAmount = baseTokenAmount * withdrawFee / FEE_PRECISION;
            withdrawFeeAmount += feeAmount;
            baseTokenAmount = baseTokenAmount - feeAmount;
        }

        baseToken.safeTransfer(data.receiver, baseTokenAmount);
        emit ExecuteWithdraw(
            firstTemp, data.owner, data.receiver, shares, data.amount, baseTokenAmount, block.timestamp
        );
        delete withdrawQueue.queue[firstTemp];
        // increment the first index of the withdraw queue
        firstTemp += 1;
    }
    totalWithdrawnAmount += processedBaseTokenAmount;

    if (withdrawFeeAmount > 0) {
        baseToken.safeTransfer(withdrawFeeReceiver, withdrawFeeAmount);
    }
    ...
}
```

`withdrawFeeReceiver` will be `NoyaFeeReceiver.sol`, but the `FeeReceiver` contract cannot instantly redeem the fees to the `receiver` address, instead it can only initiate a new withdraw flow from the `AccountingManager` like a normal user, which in the end will deduct a fee again over the fees that he requests to redeem.

[NoyaFeeReceiver.sol#L7-L30](https://github.com/code-423n4/2024-04-noya/blob/9c79b332eff82011dcfa1e8fd51bad805159d758/contracts/accountingManager/NoyaFeeReceiver.sol#L7-L30)

```solidity
contract NoyaFeeReceiver is Ownable {
    address public receiver;
    address public accountingManager;
    address public baseToken;

    event ManagementFeeReceived(address indexed token, uint256 amount);

    constructor(address _accountingManager, address _baseToken, address _receiver) Ownable(msg.sender) {
        require(_accountingManager != address(0));
        require(_baseToken != address(0));
        require(_receiver != address(0));
        accountingManager = _accountingManager;
        baseToken = _baseToken;
        receiver = _receiver;
    }

    function withdrawShares(uint256 amount) external onlyOwner {
        AccountingManager(accountingManager).withdraw(amount, receiver);
    }

    function burnShares(uint256 amount) external onlyOwner {
        AccountingManager(accountingManager).burnShares(amount);
    }
}
```

Example:

If user withdraws 1000 and the fees are 100 (10%). Then `NoyaFeeReceiver.sol` will initially withdraw for the fees, but when it comes to `executeWithdraw`, it will only send 90 to it and 10 will be sent back to `NoyaFeeReceiver.sol` as fees.

> Can think of that as in a bank. When they collect taxes, those taxes are profit for them, they don't pay tax again to the bank upon withdraw.
> 

## Tools Used

Manual Review

## Recommended Mitigation Steps

Allow `NoyaFeeReceiver.sol` to be able to cash out the fees immediately or at least skip the withdrawal fee when it requests the fees.