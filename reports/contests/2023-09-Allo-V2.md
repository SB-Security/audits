## Findings Summary

| ID | Description | Severity |
| - | - | - |
| [M-01](#m-01-setting-useregistryanchor-to-true-will-result-in-recipientaddress-always-be-address0-in-rfpsimplestrategy) | Setting `useRegistryAnchor` to true will result in recipientAddress always be `address(0)` in RfpSimpleStrategy | Medium |
| [M-02](#m-02-allosol-feeamount-will-round-down-to-0-when-user-calls-fundpool-with-small-amount) | `Allo.sol` feeAmount will round down to 0 when user calls `fundPool()` with small amount | Medium |

# [M-01] Setting `useRegistryAnchor` to true will result in recipientAddress always be `address(0)` in RfpSimpleStrategy

## Summary

DoS in the core function of one of the strategies, due to the wrong position of the if check.

## Vulnerability Detail

When a new pool with strategies of types `RfpSimple` or `RFPCommitteeStrategy` is created, there are properties set only in the `Initialize` function that **cannot be called more than once**. One of these properties is **useRegistryAnchor**, which is crucial when a user wants to use the `_registerRecipient` function in order to participate in the pool. So let’s look at the beginning of the mentioned function: 

```solidity
src: allo-v2/contracts/strategies/rfp-simple/RfpSimpleStrategy.sol

314: function _registerRecipient(bytes memory _data, address _sender)
	internal
	override
	onlyActivePool
	returns (address recipientId)
{
	bool isUsingRegistryAnchor;
	//@audit default value is address(0)
	address recipientAddress;
	address registryAnchor;
	uint256 proposalBid;
	Metadata memory metadata;

	// Decode '_data' depending on the 'useRegistryAnchor' flag
	if (useRegistryAnchor) {
	    /// @custom:data when 'true' -> (address recipientId, uint256 proposalBid, Metadata metadata)
	    (recipientId, proposalBid, metadata) = abi.decode(_data, (address, uint256, Metadata));
	
	    // If the sender is not a profile member this will revert
	    if (!_isProfileMember(recipientId, _sender)) revert UNAUTHORIZED();
	}
}
```

As we can see there are variables initialized with their default values and for **recipientAddress it is address(0).** When we look at the body inside the **if check** that will be executed when **useRegistryAnchor = true,** we can see that `recipientAddress` doesn't change at all, it's still the address(0)**.** Now let’s look at the rest of the `_registerRecipient` function and especially another **if check on line 362:** 

```solidity
if (recipientAddress == address(0)) revert RECIPIENT_ERROR(recipientId)
```

As we acknowledged in from the statement above **recipientAddress** won’t be any other address than address(0), so `_registerRecipient` will revert every time, and the fact that **useRegistryAnchor** cannot be changed, makes the whole pool unusable.

## Impact

Denial of service in the core function of the RfpSimpleStrategy, because of the variable being address(0) and there is a if check for the same case later in the function.

## Code Snippet

https://github.com/sherlock-audit/2023-09-Gitcoin/blob/6430c8004017e96ae2f5aac365bdefd0b6eeea72/allo-v2/contracts/strategies/rfp-simple/RFPSimpleStrategy.sol#L307-L380

## Tool used

Manual Review

## Recommendation

Consider moving the recipientAddress check to the if statement.

```diff
	/// @notice Submit a proposal to RFP pool
	/// @dev Emits a 'Registered()' event
	/// @param _data The data to be decoded
	/// @custom:data when 'useRegistryAnchor' is 'true' -> (address recipientId, uint256 proposalBid, Metadata metadata)
	///              when 'useRegistryAnchor' is 'false' -> (address recipientAddress, address registryAnchor, uint256 proposalBid, Metadata metadata)
	/// @param _sender The sender of the transaction
	/// @return recipientId The id of the recipient
	function _registerRecipient(bytes memory _data, address _sender)
	    internal
	    override
	    onlyActivePool
	    returns (address recipientId)
	{
	    bool isUsingRegistryAnchor;
	    address recipientAddress;
	    address registryAnchor;
	    uint256 proposalBid;
	    Metadata memory metadata;
	
	    // Decode '_data' depending on the 'useRegistryAnchor' flag
	    if (useRegistryAnchor) {
	        /// @custom:data when 'true' -> (address recipientId, uint256 proposalBid, Metadata metadata)
	        (recipientId, proposalBid, metadata) = abi.decode(_data, (address, uint256, Metadata));
	
	        // If the sender is not a profile member this will revert
	        if (!_isProfileMember(recipientId, _sender)) revert UNAUTHORIZED();
	    } else {
	        //  @custom:data when 'false' -> (address recipientAddress, address registryAnchor, uint256 proposalBid, Metadata metadata)
	        (recipientAddress, registryAnchor, proposalBid, metadata) =
	            abi.decode(_data, (address, address, uint256, Metadata));

+		// If the recipient address is the zero address this will revert
+		if (recipientAddress == address(0)) revert RECIPIENT_ERROR(recipientId);
	
	        // Check if the registry anchor is valid so we know whether to use it or not
	        isUsingRegistryAnchor = registryAnchor != address(0);
	
	        // Ternerary to set the recipient id based on whether or not we are using the 'registryAnchor' or '_sender'
	        recipientId = isUsingRegistryAnchor ? registryAnchor : _sender;
	
	        // Checks if the '_sender' is a member of the profile 'anchor' being used and reverts if not
	        if (isUsingRegistryAnchor && !_isProfileMember(recipientId, _sender)) revert UNAUTHORIZED();
	    }
	
	    // Check if the metadata is required and if it is, check if it is valid, otherwise revert
	    if (metadataRequired && (bytes(metadata.pointer).length == 0 || metadata.protocol == 0)) {
	        revert INVALID_METADATA();
	    }
	
	    if (proposalBid > maxBid) {
	        // If the proposal bid is greater than the max bid this will revert
	        revert EXCEEDING_MAX_BID();
	    } else if (proposalBid == 0) {
	        // If the proposal bid is 0, set it to the max bid
	        proposalBid = maxBid;
	    }
	
-	    // If the recipient address is the zero address this will revert
-	    if (recipientAddress == address(0)) revert RECIPIENT_ERROR(recipientId);
	
	    // Get the recipient
	    Recipient storage recipient = _recipients[recipientId];
	
	    if (recipient.recipientStatus == Status.None) {
	        // If the recipient status is 'None' add the recipient to the '_recipientIds' array
	        _recipientIds.push(recipientId);
	        emit Registered(recipientId, _data, _sender);
	    } else {
	        emit UpdatedRegistration(recipientId, _data, _sender);
	    }
	
	    // update the recipients data
	    recipient.recipientAddress = recipientAddress;
	    recipient.useRegistryAnchor = isUsingRegistryAnchor ? true : recipient.useRegistryAnchor;
	    recipient.proposalBid = proposalBid;
	    recipient.recipientStatus = Status.Pending;
	}
```

# [M-02] `Allo.sol` feeAmount will round down to 0 when user calls `fundPool()` with small amount

## Summary

Allo won’t charge fee when funding with small amounts.

## Vulnerability Detail

Allo.sol takes a fee when it funds a pool via `_fundPool()`, and the fee is applied based on the `percentFee`. The variable is represented in `e18` format as specified in the code.

> *How the percentage is represented in our contracts: 1e18 = 100%, 1e17 = 10%, 1e16 = 1%, 1e15 = 0.1%*

So if Allo is, say, with 1e15 (0.1%), we need to pass at least 1000 as an amount in order for Allo to take a fee, any other numbers below that will round the calculation of fee Amount to 0.

No matter what `percentFee` is currently set. The amount when fund a pool, must be -

*`1e18 /* percentFee`  or *`1e(18 - percentFee decimals)`*

## Impact

The user can call `fundPool()` with a small amount many times and Allo will never take any fees.

## Code Snippet

```solidity
/// @notice Fund a pool.
    /// @dev Deducts the fee and transfers the amount to the distribution strategy.
    ///      Emits a 'PoolFunded' event.
    /// @param _amount The amount to transfer
    /// @param _poolId The 'poolId' for the pool you are funding
    /// @param _strategy The address of the strategy
    function _fundPool(uint256 _amount, uint256 _poolId, IStrategy _strategy) internal {
        uint256 feeAmount;
        uint256 amountAfterFee = _amount;

        Pool storage pool = pools[_poolId];
        address _token = pool.token;

        if (percentFee > 0) {
            feeAmount = (_amount * percentFee) / getFeeDenominator();
            amountAfterFee -= feeAmount;

            _transferAmountFrom(_token, TransferData({from: msg.sender, to: treasury, amount: feeAmount}));
        }

        _transferAmountFrom(_token, TransferData({from: msg.sender, to: address(_strategy), amount: amountAfterFee}));
        _strategy.increasePoolAmount(amountAfterFee);

        emit PoolFunded(_poolId, amountAfterFee, feeAmount);
    }
```
## Tool used

Manual Review

## Recommendation

Use other *`percentFee`* representation.