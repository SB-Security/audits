## Findings Summary

| ID | Description | Severity |
| - | - | - |
| [H-01](#h-01-decentbridgeexecutor-will-refund-to-the-decentethrouter-source-chain-instead-of-the-passed-refunder-if-execute-is-not-successful) | `DecentBridgeExecutor` will refund to the `DecentEthRouter` source chain instead of the passed refunder if execute is not successful | High |
| [L-01](#l-01-zropaymentaddress-should-not-be-hardcoded-to-address0) | `zroPaymentAddress` should not be hardcoded to `address(0)` | Low |
| [L-02](#l-02-do-not-hardcode-_usezro-to-false-when-estimating-layerzero-fees) | Do not hardcode `_useZro` to false when estimating layerZero fees | Low |
| [L-03](#l-03-off-by-one-will-prevent-both-weth-and-eth-full-redeems) | Off-by one will prevent full redeems in `DecentEthRouter` | Low |

# [H-01] `DecentBridgeExecutor` will refund to the `DecentEthRouter` source chain instead of the passed refunder if execute is not successful

## Impact

Funds will be stuck in `DecentBridgeAdapter`'s source chain address if the transaction fails on the destination chain due to the wrong address passed as `refunder` in `DecentBridgeExecutor::execute`. The issue is that the funds will be refunded to the **`_from`** address, which is the **`DecentBridgeAdapter`**'s address on the source chain. That will result in locked funds without a way to retrieve them because `DecentBridgeAdapter`'s address passed is not on the chain where the transaction is executed.

If there is no contract deployed on this address, attacker can deterministically deploy on it and either wait for refund tokens to accumulate or immediately steal them.

## Proof of Concept

We can ensure that the `from` parameter encoded will be the source chain address of `DecentBridgeAdapter` which is encoded and passed to the destination chain to be processed in the `DecentBridgeAdapter` by looking at the code:

1. User initiates `bridgeAndExecute` function in `UTB` and passes `Bob` as refund address.
2. `DecentBridgeAdapter::bridge` is called to modify the payload which will call the `receiveFromBridge` on the destination chain when `LayerZero` processes the transactions.
3. `bridge` function ends up calling the `DecentEthRouter::bridgeWithPayload` which will initiate the execution
4. Then in `DecentEthRouter::bridgeWithPayload`, `_getCallParams` will be called to encode the call params.

> When we look inside `_getCallParams` we can see that the **second argument encoded is** `msg.sender` **(DecentBridgeAdapter)** which ends up being the address `_from` decoded later on in `DecentEthRouter::onOFTReceived` in the destination chain:
> 

[DecentEthRouter.sol#L80-L111](https://github.com/decentxyz/decent-bridge/blob/7f90fd4489551b69c20d11eeecb17a3f564afb18/src/DecentEthRouter.sol#L80-L111)

```solidity
function _getCallParams(
        uint8 msgType,
        address _toAddress,
        uint16 _dstChainId,
        uint64 _dstGasForCall,
        bool deliverEth,
        bytes memory additionalPayload
    )
        private
        view
        returns (
            bytes32 destBridge,
            bytes memory adapterParams,
            bytes memory payload
        )
    {
        uint256 GAS_FOR_RELAY = 100000;
        uint256 gasAmount = GAS_FOR_RELAY + _dstGasForCall;
        adapterParams = abi.encodePacked(PT_SEND_AND_CALL, gasAmount);
        destBridge = bytes32(abi.encode(destinationBridges[_dstChainId]));
        if (msgType == MT_ETH_TRANSFER) {
****            payload = abi.encode(msgType, msg.sender, _toAddress, deliverEth);
        } else {
**-->below: encoded data: MT_ETH_TRANSFER_WITH_PAYLOAD, DecentBridgeAdapter (source), DecentBridgeAdapter (dest), false**
            payload = abi.encode(
                msgType,
                msg.sender,
                _toAddress,
                deliverEth,
                additionalPayload
            );
        }
    }
```

[DecentEthRouter.sol#L237-L282](https://github.com/decentxyz/decent-bridge/blob/7f90fd4489551b69c20d11eeecb17a3f564afb18/src/DecentEthRouter.sol#L237-L282)

```solidity
function onOFTReceived(
        uint16 _srcChainId,
        bytes calldata,
        uint64,
        bytes32,
        uint _amount,
        bytes memory _payload
    ) external override onlyLzApp {
--> below: decode the payload passed to 
        (uint8 msgType, address _from, address _to, bool deliverEth) = abi
            .decode(_payload, (uint8, address, address, bool));
        bytes memory callPayload = "";

        if (msgType == MT_ETH_TRANSFER_WITH_PAYLOAD) {
            (, , , , callPayload) = abi.decode(
                _payload,
                (uint8, address, address, bool, bytes)
            );
        }
        emit ReceivedDecentEth(
            msgType,
            _srcChainId,
            _from,
            _to,
            _amount,
            callPayload
        );

        if (weth.balanceOf(address(this)) < _amount) {
            dcntEth.transfer(_to, _amount);
            return;
        }

        if (msgType == MT_ETH_TRANSFER) {
            if (!gasCurrencyIsEth || !deliverEth) {
                weth.transfer(_to, _amount);
            } else {
                weth.withdraw(_amount);
                payable(_to).transfer(_amount);
            }
        } else {
--> below: call **DecentBridgeExecutor's execute function**
            weth.approve(address(executor), _amount);
            executor.execute(_from, _to, deliverEth, _amount, callPayload);
        }
    }
```

Since we call with `msgType` = `MT_ETH_TRANSFER_WITH_PAYLOAD` we can see that the `execute` will be called with the following params: 

- `_from`: `DecentBridgeAdaptor` on source chain
- `_to`:  `DecentBridgeAdaptor` on destination chain
- `deliverEth`: `false`
- `callPayload`: encoded `receiveFromBridge` execution instructions

Then we look at `DecentBridgeExecutor::execute` and especially `_executeWeth` since the `deliverEth` = false: 

[DecentBridgeExecutor.sol#L24-L45](https://github.com/decentxyz/decent-bridge/blob/7f90fd4489551b69c20d11eeecb17a3f564afb18/src/DecentBridgeExecutor.sol#L24-L45)

```solidity
function _executeWeth(
    address from,
    address target,
    uint256 amount,
    bytes memory callPayload
) private {
    uint256 balanceBefore = weth.balanceOf(address(this));
    weth.approve(target, amount);

    (bool success, ) = target.call(callPayload);

    if (!success) {
--> below: **from is DecentBridgeAdapter on source chain**
        weth.transfer(from, amount);
        return;
    }

    uint256 remainingAfterCall = amount -
        (balanceBefore - weth.balanceOf(address(this)));

    // refund the sender with excess WETH
    weth.transfer(from, remainingAfterCall);
}
```

If the result of the low-level call is false it will try to refund the total amount to the `from` address, which is the address of `DecentBridgeAdapter`, but on the source chain.

> ***NOTE:** Even if the contracts are deployed using `CREATE2`, they won't have the same addresses because their constructor parameters must be different for proper configuration. However, for an attacker who only needs to obtain the same address, it won't be difficult to precompute it.*
> 

## Tools Used

Manual Review

## Recommended Mitigation Steps

Extend the payload to pass refunder, instead of assigning `DecentBridgeAdapter`. Modify the `DecentBridgeExecutor` to refund to this address in both `_executeWeth` and `_executeEth` functions.

# [L-01] `zroPaymentAddress` should not be hardcoded to `address(0)`

**Issue Description:** In `DecentEthRouter::_bridgeWithPayload`, `zroPaymentAddress` is hardcoded to `address(0)` which is wrong according to the LayerZero integration checklist.

> *"Do not hardcode address zero `address(0)` as zroPaymentAddress when estimating fees and sending messages. Pass it as a parameter instead."*
> 

```solidity
ICommonOFT.LzCallParams memory callParams = ICommonOFT.LzCallParams({
    refundAddress: payable(msg.sender),
    zroPaymentAddress: address(0x0), //@audit should not be hardcoded
    adapterParams: adapterParams
});
```

**Recommendation:** Pass ****`zroPaymentAddress` as parameter

# [L-02] Do not hardcode `_useZro` to false when estimating layerZero fees

**Issue Description:** In `DecentEthRouter::estimateSendAndCallFee`, `_useZro` is hardcoded to `false` which is wrong according to the LayerZero integration checklist.

> *"Do not hardcode `useZro` to `false` when estimating fees and sending messages. Pass it as a parameter instead."*
> 

```solidity
function estimateSendAndCallFee(
    uint8 msgType,
    uint16 _dstChainId,
    address _toAddress,
    uint _amount,
    uint64 _dstGasForCall,
    bool deliverEth,
    bytes memory payload
) public view returns (uint nativeFee, uint zroFee) {
    (
        bytes32 destinationBridge,
        bytes memory adapterParams,
        bytes memory _payload
    ) = _getCallParams(
            msgType,
            _toAddress,
            _dstChainId,
            _dstGasForCall,
            deliverEth,
            payload
        );

    return
        dcntEth.estimateSendAndCallFee(
            _dstChainId,
            destinationBridge,
            _amount,
            _payload,
            _dstGasForCall,
            false, // useZero // @audit should not be hardcoded
            adapterParams // Relayer adapter parameters
            // contains packet type (send and call in this case) and gasAmount
        );
}
```

**Recommendation:** Pass ****`_useZro` as parameter

# [L-03] Off-by one will prevent both `WETH` and `ETH` full redeems.

**Issue Description:** 

This functions will be used to when there is not enough liquidity when the bridge message reaches the destination chain user will be refunded in form of dcntETH token and he will have to redeem the in either one of the functions: 

- `redeemEth`
- `redeemWeth`

Both have `onlyIfWeHaveEnoughReserves` modifier applied:

```solidity
modifier onlyIfWeHaveEnoughReserves(uint256 amount) {
        require(weth.balanceOf(address(this)) > amount, "not enough reserves");
        _;
    }
```

Since this contract will not hold any excess `WETH` and `ETH`, user who redeems will always lose 1 wei. 

**Recommendation:** 

**Modify the** `onlyIfWeHaveEnoughReserves`:

```diff
modifier onlyIfWeHaveEnoughReserves(uint256 amount) {
-        require(weth.balanceOf(address(this)) > amount, "not enough reserves");
+        require(weth.balanceOf(address(this)) >= amount, "not enough reserves");
        _;
    }
```